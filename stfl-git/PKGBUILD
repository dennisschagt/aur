# Maintainer: Dennis van der Schagt <dennisschagt@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=stfl-git
pkgver=0.24.r2.gb7be405
pkgrel=1
pkgdesc="Library implementing a curses-based widget set for text terminals"
arch=('x86_64')
url="http://clifford.at/stfl/"
license=('GPL3')
conflicts=('stfl')
provides=('stfl')
depends=('ncurses')
makedepends=('git' 'patch' 'swig')
source=("$pkgname::git+https://github.com/dennisschagt/stfl.git#branch=master"
        stfl-archlinux.patch)
sha256sums=('SKIP'
            'c7d7c787bcd287a776aad3b26e6a90a3b121c281125c34c7c35a87f36dcdc453')
OPTIONS+=(debug !strip)

pkgver() {
  cd "$pkgname"
  git describe --long | sed 's/^r//;s/-/.r/;s/-/./'
}

build() {
  cd "${srcdir}"/$pkgname

  patch -Np1 -i "${srcdir}"/stfl-archlinux.patch

  make prefix=/usr Makefile.deps
  make prefix=/usr
}

package() {
  cd "${srcdir}"/$pkgname

  make prefix=/usr DESTDIR="${pkgdir}" install

#library symlink - FS#34890
  ln -s /usr/lib/libstfl.so.0.24 "${pkgdir}"/usr/lib/libstfl.so.0
}
